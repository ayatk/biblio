/*
 * Copyright (c) 2016-2017 Aya Tokikaze. All Rights Reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'

def APP_NAME = "biblio"
def VERSION = "0.2.2-alpha"
def PACKAGE_NAME = "com.ayatk.biblio"

// for debug info
def GIT_SHA = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def GIT_BRANCH = (System.getenv("CIRCLE_BRANCH") != null) ? System.getenv("CIRCLE_BRANCH") :
    'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
def BUILD_NUM = (System.getenv("CIRCLE_BUILD_NUM") != null) ? System.getenv("CIRCLE_BUILD_NUM") : "1"
def BUILD_TIME = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

// Keys
def RELEASE_STORE_PASSWORD = (System.getenv("RELEASE_STORE_PASSWORD") != null) ?
    System.getenv("RELEASE_STORE_PASSWORD") : ""
def RELEASE_KEY_ALIAS = (System.getenv("RELEASE_KEY_ALIAS") != null) ?
    System.getenv("RELEASE_KEY_ALIAS") : ""
def RELEASE_KEY_PASSWORD = (System.getenv("RELEASE_KEY_PASSWORD") != null) ?
    System.getenv("RELEASE_KEY_PASSWORD") : ""
def FABRIC_API_KEY = (System.getenv("FABRIC_API_KEY") != null) ?
    System.getenv("FABRIC_API_KEY") : ""

def MIN_SDK_VERSION = 21 // Lollipop

def secretPropsFile = file("$rootDir/secret.properties")
if (secretPropsFile.canRead()) {
  Properties secretProps = new Properties()
  secretProps.load(new FileInputStream(secretPropsFile))
  RELEASE_STORE_PASSWORD = secretProps['RELEASE_STORE_PASSWORD']
  RELEASE_KEY_ALIAS = secretProps['RELEASE_KEY_ALIAS']
  RELEASE_KEY_PASSWORD = secretProps['RELEASE_KEY_PASSWORD']
  FABRIC_API_KEY = secretProps['FABRIC_API_KEY']
}

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.1'

  defaultConfig {
    applicationId PACKAGE_NAME
    minSdkVersion MIN_SDK_VERSION
    targetSdkVersion 27
    versionCode BUILD_NUM.toInteger()
    versionName VERSION
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary true
    multiDexEnabled true

    buildConfigField "String", "GIT_SHA", "\"${GIT_SHA}\""
    buildConfigField "String", "GIT_BRANCH", "\"${GIT_BRANCH}\""
    buildConfigField "String", "BUILD_NUM", "\"${BUILD_NUM}\""
    buildConfigField "String", "BUILD_TIME", "\"${BUILD_TIME}\""
    manifestPlaceholders = [FABRIC_API_KEY: FABRIC_API_KEY]
  }

  signingConfigs {
    release {
      storeFile file("$rootDir/app.release.jks")
      storePassword RELEASE_STORE_PASSWORD
      keyAlias RELEASE_KEY_ALIAS
      keyPassword RELEASE_KEY_PASSWORD
    }
    debug {
      storeFile file("$rootDir/app.debug.jks")
      storePassword 'android'
      keyAlias 'debug'
      keyPassword 'android'
    }
  }

  buildTypes {
    applicationVariants.all { variant ->
      variant.outputs.all { output ->
        if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
          outputFileName = "${APP_NAME}-v${VERSION}.apk"
        }
      }
    }

    release {
      shrinkResources true
      debuggable false
      zipAlignEnabled true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }

    debug {
      minifyEnabled false
      debuggable true
      zipAlignEnabled true
      applicationIdSuffix '.debug'
      versionNameSuffix "-debug"
      signingConfig signingConfigs.debug
    }
  }

  productFlavors {
    develop {}
    // use deploygate
    nightly {
      minSdkVersion MIN_SDK_VERSION
      applicationId "${PACKAGE_NAME}.nightly"
      versionName "${VERSION}-nightly"
    }
    production {
      // The actual minSdkVersion for the application.
      minSdkVersion MIN_SDK_VERSION
    }
  }

  variantFilter { variant ->
    def names = variant.flavors*.name

    def nightlyDebug = names.contains("nightly") && variant.buildType.name == "debug"
    def productionDebug = names.contains("production") && variant.buildType.name == "debug"
    // To check for a certain build type, use variant.buildType.name == "<buildType>"
    if (nightlyDebug || productionDebug) {
      // Gradle ignores any variants that satisfy the conditions above.
      setIgnore(true)
    }
  }

  flavorDimensions 'tier'

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/MANIFEST.MF'
  }

  dataBinding {
    enabled = true
  }

  kapt {
    // NonExistentClass Errorを無視
    correctErrorTypes = true
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    // ディープリンクは利用しないので、無効化
    disable 'GoogleAppIndexingWarning'
    // InvalidPackageのエラーを除外
    disable 'InvalidPackage'
    disable 'RestrictedApi'
  }

  testOptions {
    unitTests.all {
      // All the usual Gradle options.
      testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        exceptionFormat "full"
      }
    }
  }
}

configurations {
  ktlint
}

ext.supportLibVersion = '27.0.1'

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  // android support libraries
  implementation "com.android.support:appcompat-v7:${supportLibVersion}"
  implementation "com.android.support:design:${supportLibVersion}"
  implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
  implementation "com.android.support:cardview-v7:${supportLibVersion}"
  implementation "com.android.support:customtabs:${supportLibVersion}"
  implementation "com.android.support:preference-v7:${supportLibVersion}"
  implementation "com.android.support:preference-v14:${supportLibVersion}"
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'
  kapt "com.android.databinding:compiler:3.0.0"

  // Android Architecture Components
  implementation "android.arch.lifecycle:runtime:1.0.3"
  implementation "android.arch.lifecycle:extensions:1.0.0"
  implementation "android.arch.lifecycle:reactivestreams:1.0.0"
  kapt "android.arch.lifecycle:compiler:1.0.0"

  // Network
  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
  implementation 'com.google.code.gson:gson:2.8.2'
  implementation 'com.squareup.okhttp3:okhttp:3.9.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

  // Dependency Injection
  implementation 'com.google.dagger:dagger:2.13'
  kapt 'com.google.dagger:dagger-compiler:2.13'
  implementation 'com.google.dagger:dagger-android:2.13'
  kapt 'com.google.dagger:dagger-android-processor:2.13'
  implementation 'com.google.dagger:dagger-android-support:2.13'

  // ReactiveX
  implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

  // O/R Mapping
  implementation 'com.github.gfx.android.orma:orma:4.2.5'
  kapt 'com.github.gfx.android.orma:orma-processor:4.2.5'

  // SharedPreferences
  implementation 'com.rejasupotaro:kvs-schema:5.0.1'
  kapt 'com.rejasupotaro:kvs-schema-compiler:5.0.1'

  // Jsoup
  implementation 'org.jsoup:jsoup:1.11.1'

  // UI
  implementation 'com.google.android:flexbox:0.3.1'
  implementation 'com.github.mabbas007:TagsEditText:1.0.5'

  // Util
  implementation 'org.parceler:parceler-api:1.1.9'
  kapt 'org.parceler:parceler:1.1.9'
  implementation 'org.greenrobot:eventbus:3.0.0'

  // Debug
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
  testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
  implementation 'com.jakewharton.timber:timber:4.6.0'
  debugImplementation 'com.facebook.stetho:stetho:1.5.0'
  debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
  debugImplementation 'com.facebook.stetho:stetho-timber:1.5.0@aar'
  debugImplementation 'com.tomoima.debot:debot:2.0.3'
  releaseImplementation 'com.tomoima.debot:debot-no-op:2.0.3'
  debugImplementation 'jp.wasabeef:takt:1.0.5'
  implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
    transitive = true
  }

  androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testImplementation 'junit:junit:4.12'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

  // Lint
  ktlint "com.github.shyiko:ktlint:0.12.1"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args "src/**/*.kt"
  // to generate report in checkstyle format prepend following args:
  // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'
